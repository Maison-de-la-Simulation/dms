cmake_minimum_required(VERSION 3.1)

project(distributed_memory_system VERSION 1.0 LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

list(APPEND CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake)
include(DmsCompileOptions)

set(SESSIONS SERIAL_SESSION MPI_SESSION)
set(SESSION SERIAL_SESSION CACHE STRING "Session type. Options: ${SESSIONS}")
set_property(CACHE SESSION PROPERTY STRINGS ${SESSIONS})

option(dms_ENABLE_TESTS "Build dms tests" OFF)

if(${SESSION} STREQUAL MPI_SESSION)
  find_package(MPI REQUIRED COMPONENTS CXX)
  if(NOT TARGET MPI::MPI_CXX)
    add_library(MPI::MPI_CXX IMPORTED INTERFACE)
    set_property(TARGET MPI::MPI_CXX PROPERTY INTERFACE_COMPILE_OPTIONS ${MPI_CXX_COMPILE_FLAGS})
    set_property(TARGET MPI::MPI_CXX PROPERTY INTERFACE_INCLUDE_DIRECTORIES ${MPI_CXX_INCLUDE_PATH})
    set_property(TARGET MPI::MPI_CXX PROPERTY INTERFACE_LINK_LIBRARIES ${MPI_CXX_LIBRARIES})
    if (${MPI_CXX_LINK_FLAGS})
      string(STRIP ${MPI_CXX_LINK_FLAGS} MPI_CXX_LINK_FLAGS)
      set_property(TARGET MPI::MPI_CXX APPEND PROPERTY INTERFACE_LINK_LIBRARIES ${MPI_CXX_LINK_FLAGS})
    endif(${MPI_CXX_LINK_FLAGS})
  endif(NOT TARGET MPI::MPI_CXX)
endif(${SESSION} STREQUAL MPI_SESSION)

add_subdirectory(src)

if(dms_ENABLE_TESTS)
  enable_testing()
  add_subdirectory(test)
endif(dms_ENABLE_TESTS)
